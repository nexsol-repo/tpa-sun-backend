plugins {
    id 'java-library'
    id 'jacoco'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)

}

apply from: 'lint.gradle'

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"
    description = "${projectDescription}"
    java{
    sourceCompatibility = project.javaVersion
        }
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'
    apply plugin: 'java-test-fixtures'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }


    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")
        testImplementation("org.assertj:assertj-core:3.27.3")
        testAnnotationProcessor("org.projectlombok:lombok:1.18.42")
        testCompileOnly("org.projectlombok:lombok:1.18.42")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testImplementation("org.springframework.boot:spring-boot-starter-webmvc-test")
        testImplementation("org.springframework.boot:spring-boot-starter-data-jpa-test")
        testImplementation("org.springframework.boot:spring-boot-starter-mail-test")
        testImplementation("org.springframework.boot:spring-boot-starter-validation-test")
        testImplementation("org.springframework.boot:spring-boot-starter-security-test")



    }

    bootJar.enabled = false
    jar.enabled = true


    test {
        useJUnitPlatform {
            excludeTags('develop', 'restdocs')
            jvmArgs("-XX:+EnableDynamicAgentLoading")
        }
    }

    tasks.register('unitTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            excludeTags('develop', 'context', 'restdocs')
        }
    }

    tasks.register('contextTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('context')
        }
    }

    tasks.register('restDocsTest', Test) {
        group = 'verification'
        description = 'Runs REST Docs tests.'

        useJUnitPlatform {
            includeTags('restdocs')
        }

        // 컴파일이 먼저 수행되도록 의존성 명시
        dependsOn 'testClasses'

        // 테스트 클래스 위치 지정
        testClassesDirs = sourceSets.test.output.classesDirs

        // [핵심 해결책] 런타임 클래스패스에 '메인 소스셋의 출력(컴파일된 클래스들)'을 명시적으로 추가
        classpath = sourceSets.test.runtimeClasspath + sourceSets.main.output

        testLogging {
            events "passed", "skipped", "failed"
            showStandardStreams = true
        }
    }


    tasks.register('developTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('develop')
        }
    }

    tasks.named('asciidoctor') {
        dependsOn restDocsTest
        inputs.dir file('build/generated-snippets')
    }
    bootJar {
        dependsOn asciidoctor

        from("${asciidoctor.outputDir}"){
            into 'static/docs'
        }
    }

    tasks.register('copyDocsToResources', Copy) {
        dependsOn asciidoctor
        from asciidoctor.outputDir
        into file("build/resources/main/static/docs") // 실행되는 클래스패스 위치
    }

    tasks.named('bootRun') {
        dependsOn 'copyDocsToResources'
    }
}
